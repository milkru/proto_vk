#version 460

#extension GL_EXT_control_flow_attributes: require
#extension GL_EXT_shader_8bit_storage: require
#extension GL_EXT_shader_16bit_storage: require
#extension GL_EXT_shader_explicit_arithmetic_types_int8 : require
#extension GL_NV_mesh_shader: require
#extension GL_KHR_shader_subgroup_ballot: require
#extension GL_KHR_shader_subgroup_vote: require

#include "shader_common.h"

layout(local_size_x = kShaderGroupSizeNV) in;

layout(binding = 0) readonly buffer PerDrawDataVector { PerDrawData perDrawDataVector[]; };
layout(binding = 1) readonly buffer DrawCommands { DrawCommand drawCommands[]; };
layout(binding = 2) readonly buffer Meshlets { Meshlet meshlets[]; };
layout(binding = 3) readonly buffer Meshes { Mesh meshes[]; };

taskNV out Task
{
	uint meshletIndices[kShaderGroupSizeNV];
} outTask;

layout (push_constant) uniform block
{
    PerFrameData perFrameData;
};

void main()
{
	uint drawIndex = drawCommands[gl_DrawID].drawIndex;
	PerDrawData perDrawData = perDrawDataVector[drawIndex];
	
	Mesh mesh = meshes[perDrawData.meshIndex];
	uint subsetIndex = drawCommands[gl_DrawID].subsetIndex;
	MeshSubset subset = mesh.subsets[subsetIndex];
	
	uint lodIndex = drawCommands[gl_DrawID].lodIndex;
	MeshLod meshLod = subset.lods[lodIndex];

	uint groupThreadIndex = gl_LocalInvocationID.x;
	uint localMeshletIndex = gl_GlobalInvocationID.x;
	uint meshletIndex = meshLod.meshletOffset + localMeshletIndex;
	
	if (localMeshletIndex >= meshLod.meshletCount)
	{
		return;
	}

	vec3 coneApex = (perDrawData.model * vec4(
		meshlets[meshletIndex].center[0],
		meshlets[meshletIndex].center[1],
		meshlets[meshletIndex].center[2], 1.0)).xyz;
	
	vec3 center = (perDrawData.model * vec4(
		int(meshlets[meshletIndex].coneAxis[0]) / 127.0,
		int(meshlets[meshletIndex].coneAxis[1]) / 127.0,
		int(meshlets[meshletIndex].coneAxis[2]) / 127.0, 0.0)).xyz;

	vec3 cameraPosition = perFrameData.cameraPosition;
	float coneCutoff = int(meshlets[meshletIndex].coneCutoff) / 127.0;
	
	bool bVisible = true;
	
	bool bConeCullingEnabled = perFrameData.bEnableMeshletConeCulling == 1;
	if (subgroupAny(bConeCullingEnabled))
	{
		if (bVisible)
		{
			bool bConeCulled = dot(normalize(coneApex - cameraPosition), center) >= coneCutoff;
			bVisible = bVisible && !bConeCulled;
		}
	}
	
	bool bFrustumCullingEnabled = perFrameData.bEnableMeshletFrustumCulling == 1;
	if (subgroupAny(bFrustumCullingEnabled))
	{
		if (bVisible)
		{
			bool bFrustumCulled = false;

			[[unroll]]
			for(int i = 0; i < kFrustumPlaneCount; ++i)
			{
				bFrustumCulled = bFrustumCulled ||
					dot(vec4(coneApex, 1.0), perFrameData.frustumPlanes[i]) + meshlets[meshletIndex].radius < 0.0;
			}

			bVisible = bVisible && !bFrustumCulled;
		}
	}

	uvec4 visibleBallot = subgroupBallot(bVisible);
	
	if (bVisible)
	{
		uint subgroupLocalMeshletIndex = subgroupBallotExclusiveBitCount(visibleBallot);
		outTask.meshletIndices[subgroupLocalMeshletIndex] = meshletIndex;
	}
	
	if (groupThreadIndex == 0)
	{
		uint visibleMeshletCount = subgroupBallotBitCount(visibleBallot);
		gl_TaskCountNV = visibleMeshletCount;
	}
}
