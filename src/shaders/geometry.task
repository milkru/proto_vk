#version 460

#extension GL_EXT_control_flow_attributes: require
#extension GL_EXT_shader_explicit_arithmetic_types_int8 : require
#extension GL_EXT_mesh_shader: require
#extension GL_KHR_shader_subgroup_ballot: require

#include "common.h"

layout(local_size_x = kShaderGroupSize) in;

layout(binding = 0) uniform UniformBufferObject { PerFrameData perFrameData; };
layout(binding = 1) readonly buffer PerDrawDataVector { PerDrawData perDrawDataVector[]; };
layout(binding = 2) readonly buffer DrawCommands { DrawCommand drawCommands[]; };
layout(binding = 3) readonly buffer Meshlets { Meshlet meshlets[]; };
layout(binding = 4) readonly buffer Meshes { Mesh meshes[]; };
layout(binding = 8) buffer MeshletVisibility { u32 meshletVisibility[]; };
layout(binding = 9) uniform sampler2D hzb;

layout (push_constant) uniform block
{
    PerPassData perPassData;
};

taskPayloadSharedEXT u32 meshletIndices[kShaderGroupSize];

void main()
{
	u32 drawIndex = drawCommands[gl_DrawID].drawIndex;
	PerDrawData perDrawData = perDrawDataVector[drawIndex];
	
	Mesh mesh = meshes[perDrawData.meshIndex];
	
	u32 lodIndex = drawCommands[gl_DrawID].lodIndex;
	MeshLod meshLod = mesh.lods[lodIndex];

	u32 groupThreadIndex = gl_LocalInvocationID.x;
	u32 localMeshletIndex = gl_GlobalInvocationID.x;
	u32 meshletIndex = meshLod.meshletOffset + localMeshletIndex;
	
	if (localMeshletIndex >= meshLod.meshletCount)
	{
		return;
	}
	
	bool bPrepass = perPassData.bPrepass == 1;

	v3 center = (perDrawData.model * v4(
		meshlets[meshletIndex].center[0],
		meshlets[meshletIndex].center[1],
		meshlets[meshletIndex].center[2], 1.0)).xyz;
	
	// TODO-MILKRU: Multiply meshlet bounding spheres by scale

	v3 coneAxis = (perDrawData.model * v4(
		i32(meshlets[meshletIndex].coneAxis[0]) / 127.0,
		i32(meshlets[meshletIndex].coneAxis[1]) / 127.0,
		i32(meshlets[meshletIndex].coneAxis[2]) / 127.0, 0.0)).xyz;

	v3 cameraPosition = perFrameData.freezeCameraPosition.xyz;
	f32 coneCutoff = i32(meshlets[meshletIndex].coneCutoff) / 127.0;
	
	u32 meshletVisibilityIndex = perDrawData.meshletVisibilityOffset + localMeshletIndex;

	// Bit packing, where 32 referes to uint bits
	bool bPreviousVisibility = ((meshletVisibility[meshletVisibilityIndex / 32] >> (meshletVisibilityIndex % 32)) & 1u) == 1u;
	bPreviousVisibility = bPreviousVisibility && drawCommands[gl_DrawID].meshVisibility == 1; // Newly visible meshes might have stale meshlet visibility bits

	bool bOcclusionCullingEnabled = perFrameData.bMeshletOcclusionCullingEnabled == 1;
	bool bVisible = bPrepass && bOcclusionCullingEnabled ? bPreviousVisibility : true;
	
	bool bConeCullingEnabled = perFrameData.bMeshletConeCullingEnabled == 1;
	if (bVisible && bConeCullingEnabled)
	{
		bool bConeCulled = dot(normalize(center - cameraPosition), coneAxis) >= coneCutoff;
		bVisible = !bConeCulled;
	}
	
	bool bFrustumCullingEnabled = perFrameData.bMeshletFrustumCullingEnabled == 1;
	if (bVisible && bFrustumCullingEnabled)
	{
		bool bFrustumCulled = false;

		[[unroll]]
		for (i32 i = 0; i < kFrustumPlaneCount; ++i)
		{
			bFrustumCulled = bFrustumCulled ||
				dot(v4(center, 1.0), perFrameData.freezeFrustumPlanes[i]) + meshlets[meshletIndex].radius < 0.0;
		}

		bVisible = !bFrustumCulled;
	}

	if (!bPrepass)
	{
		if (bVisible && bOcclusionCullingEnabled)
		{
			v3 centerViewSpace = (perFrameData.freezeView * v4(center, 1.0)).xyz;
			f32 P00 = perFrameData.projection[0][0];
			f32 P11 = perFrameData.projection[1][1];
			f32 zNear = perFrameData.projection[3][2];
			v4 AABB;

			if (tryCalculateSphereBounds(centerViewSpace, meshlets[meshletIndex].radius, zNear, P00, P11, AABB))
			{
				f32 boundsWidth = (AABB.z - AABB.x) * f32(perFrameData.hzbSize);
				f32 boundsHeight = (AABB.w - AABB.y) * f32(perFrameData.hzbSize);
				f32 mipIndex = floor(log2(max(boundsWidth, boundsHeight)));

				f32 occluderDepth = textureLod(hzb, 0.5 * (AABB.xy + AABB.zw), mipIndex).x;
				f32 nearestBoundsDepth = zNear / (-centerViewSpace.z - meshlets[meshletIndex].radius);

				bool bOcclusionCulled = occluderDepth >= nearestBoundsDepth;
				bVisible = !bOcclusionCulled;
			}
		}
	}
	
	// TODO-MILKRU: Ballot with group share memory optimisation?
	if (!bPrepass && bOcclusionCullingEnabled)
	{
		// Bit packing, where 32 referes to uint bits
		if (bVisible)
		{
			atomicOr(meshletVisibility[meshletVisibilityIndex / 32], 1u << meshletVisibilityIndex % 32);
		}
		else
		{
			atomicAnd(meshletVisibility[meshletVisibilityIndex / 32], ~(1u << meshletVisibilityIndex % 32));
		}
	}
	
	bool bDrawMeshlet = bPrepass || !bOcclusionCullingEnabled ? bVisible : bVisible && !bPreviousVisibility;
	uv4 drawMeshletBallot = subgroupBallot(bDrawMeshlet);
	
	if (bDrawMeshlet)
	{
		u32 subgroupLocalMeshletIndex = subgroupBallotExclusiveBitCount(drawMeshletBallot);
		meshletIndices[subgroupLocalMeshletIndex] = meshletIndex;
	}
	
	u32 drawnMeshletCount = subgroupBallotBitCount(drawMeshletBallot);
	EmitMeshTasksEXT(drawnMeshletCount, 1, 1);
}
